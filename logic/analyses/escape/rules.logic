AccessPath(?ap1),
AccessPath:FromVar[?to] = ?ap1,
AccessPath:ToVar[?ap1] = ?to,
AccessPath(?ap2),
AccessPath:FromVar[?from] = ?ap2,
AccessPath:ToVar[?ap2] = ?from,
_DirectAlias(?ap1, ?ap2, ?insn) <-
  AssignLocal:Insn(?insn),
  AssignInstruction:To[?insn] = ?to,
  AssignLocal:From[?insn] = ?from.

AccessPath(?ap1),
AccessPath:FromInstanceFld[?base, ?fld] = ?ap1,
AccessPath:ToInstanceFld:Base[?ap1] = ?base,
AccessPath:ToInstanceFld:Fld[?ap1] = ?fld,
AccessPath(?ap2),
AccessPath:FromVar[?from] = ?ap2,
AccessPath:ToVar[?ap2] = ?from,
_DirectAlias(?ap1, ?ap2, ?insn) <-
  StoreInstanceField:Insn(?insn),
  StoreInstanceField:Base[?insn] = ?base,
  FieldInstruction:Signature[?insn] = ?fld,
  StoreInstanceField:From[?insn] = ?from.

AccessPath(?ap1),
AccessPath:FromStaticFld[?fld] = ?ap1,
AccessPath:ToStaticFld[?ap1] = ?fld,
AccessPath(?ap2),
AccessPath:FromVar[?from] = ?ap2,
AccessPath:ToVar[?ap2] = ?from,
_DirectAlias(?ap1, ?ap2, ?insn) <-
  StoreStaticField:Insn(?insn),
  FieldInstruction:Signature[?insn] = ?fld,
  StoreStaticField:From[?insn] = ?from.

AccessPath(?ap1),
AccessPath:FromVar[?to] = ?ap1,
AccessPath:ToVar[?ap1] = ?to,
AccessPath(?ap2),
AccessPath:FromInstanceFld[?base, ?fld] = ?ap2,
AccessPath:ToInstanceFld:Base[?ap2] = ?base,
AccessPath:ToInstanceFld:Fld[?ap2] = ?fld,
_DirectAlias(?ap1, ?ap2, ?insn) <-
  LoadInstanceField:Insn(?insn),
  LoadInstanceField:To[?insn] = ?to,
  LoadInstanceField:Base[?insn] = ?base,
  FieldInstruction:Signature[?insn] = ?fld.

AccessPath(?ap1),
AccessPath:FromVar[?to] = ?ap1,
AccessPath:ToVar[?ap1] = ?to,
AccessPath(?ap2),
AccessPath:FromStaticFld[?fld] = ?ap2,
AccessPath:ToStaticFld[?ap2] = ?fld,
_DirectAlias(?ap1, ?ap2, ?insn) <-
  LoadStaticField:Insn(?insn),
  LoadStaticField:To[?insn] = ?to,
  FieldInstruction:Signature[?insn] = ?fld.


AccessPath:ToString[?ap] = ?str<-
  AccessPath:FromVar[?var] = ?ap,
  Var:Value(?var:?str).

AccessPath:ToString[?ap] = ?str<-
  AccessPath:FromInstanceFld[?base, ?fld] = ?ap,
  Var:Value(?base:?baseStr),
  FieldSignature:Value(?fld:?fldStr),
  ?str = ?baseStr + "." + ?fldStr.

AccessPath:ToString[?ap] = ?str<-
  AccessPath:FromStaticFld[?fld] = ?ap,
  FieldSignature:Value(?fld:?str).



_PredecessorOf(?insn1, ?insn2) <-
  _DirectAlias(_, ?ap, ?insn1),
  _DirectAlias(?ap, _, ?insn2),
  BasicBlockHead[?insn1] = BasicBlockHead[?insn2],
  Instruction:Index[?insn1] > Instruction:Index[?insn2].

_PredecessorOf(?insn1, ?insn2) <-
  _DirectAlias(_, ?ap1, ?insn1),
  AccessPath:ToInstanceFld:Base[?ap1] = ?base,
  _DirectAlias(?ap2, _, ?insn2),
  AccessPath:ToVar[?ap2] = ?base,
  BasicBlockHead[?insn1] = BasicBlockHead[?insn2],
  Instruction:Index[?insn1] > Instruction:Index[?insn2].

_ImmediatePredecessorOf[?insn] = ?insn0 <- agg<<?insn0 = max(?insn_i)>>(_PredecessorOf(?insn, ?insn_i)).


Alias(?ap1, ?ap2, ?insn) <-
  _DirectAlias(?ap1, ?ap2, ?insn).

Alias(?ap1, ?ap3, ?insn1) <-
  _DirectAlias(?ap1, ?ap2, ?insn1),
  _ImmediatePredecessorOf[?insn1] = ?insn2,
  Alias(?ap2, ?ap3, ?insn2).
