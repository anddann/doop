apply plugin: 'groovy'
apply plugin: 'application'
apply plugin: 'ivy-publish'
apply from  : 'DaCapo.gradle'

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'commons-io:commons-io:2.4'
    }
}

applicationDefaultJvmArgs = ["-Xmx10g"]
//sourceCompatibility = "1.6"
//targetCompatibility = "1.6"

project.group = 'org.clyze'
project.version = 1.0

//For the doop app
mainClassName = 'org.clyze.doop.Main'


task wrapper(type: Wrapper) {
    gradleVersion = '2.14'
}

repositories {
    mavenCentral()
}

def sootClassesJar = file('lib/sootclasses-trunk.jar')

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.7',             // Groovy
            'commons-logging:commons-logging:1.1',              // Logging wrapper
            'log4j:log4j:1.2.14',                               // Logging implementation
            'commons-cli:commons-cli:1.2',                      // Command line processor
            'org.anarres:jcpp:1.4.5',                           // C preprocessor in Java
            'commons-io:commons-io:2.4',                        // File Utils
            'org.apache.ivy:ivy:2.3.0',                         // Apache Ivy (for downloading jars from maven repos)
            'org.ow2.asm:asm-debug-all:5.0.3'                   // Java Bytecode library

    compile files(sootClassesJar, 'lib/soot-infoflow.jar', 'lib/soot-infoflow-android.jar')                               // Soot is a compile time dependency

    runtime 'org.clyze:jphantom:1.2'                            // JPhantom is a runtime dependency
    runtime files('lib/averroes-no-properties.jar', 'lib/dexlib2-2.1.0-dev.jar' , 'lib/util-2.1.0-dev.jar')
}

task generateFiles(type: JavaExec) {
    description 'Generate the doop.properties & checksums.properties files'
    group = 'Other'
    main = 'org.clyze.doop.FileGenerator'
    classpath = sourceSets.main.runtimeClasspath
    args = [
        projectDir,
        sootClassesJar
    ]
}

task bytecode2jimple(type: JavaExec) {
    description 'Generate Jimple/Shimple'
    group = 'Other'
    main = 'org.clyze.doop.soot.Main'
    classpath = sourceSets.main.runtimeClasspath
    if (!project.hasProperty('args') || project.property('args').isEmpty()) {
        args "--bytecode2jimpleHelp"
    } else {
        args = ["--bytecode2jimple"] + project.property('args').tokenize()
    }
}

applicationDistribution.from(file("$projectDir/logic")) {
    into 'logic'
}

applicationDistribution.from(projectDir) {
    include 'doop.properties', 'checksums.properties', 'LICENSE'
}


run {
    dependsOn generateFiles
    //We set the DOOP_HOME environment variable (see org.clyze.doop.Main)
    environment.DOOP_HOME = projectDir
    if (project.hasProperty('args')) {
        args project.property('args').split()
    }
}

startScripts {
    dependsOn generateFiles
}

publishing {
    repositories {
        ivy {
            name 'local-ivy-repo'
            url '../ivy-repo'
        }
    }

    publications {

        doop(IvyPublication) {
            organisation "$group"
            module 'doop'
            revision "$version"
            from components.java
        }
    }
}

import org.apache.commons.io.FileUtils

clean.doFirst {
    file('logs'   ).list().each { f -> delete "logs/${f}" }
    file('out'    ).list().each { f -> delete "out/${f}" }
    file('results').list().each { f -> delete "results/${f}" }
    FileUtils.deleteQuietly(new File('last-analysis'))
}
