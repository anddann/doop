// Context-sensitive pointer analysis with context of 1 object and a
// context-sensitive heap abstraction.

RealContextFromContext[?ctx] = ?value <-
  ContextFromRealContext[?value] = ?ctx.

Context(?ctx),
ContextFromRealContext[?value] = ?ctx,
ImmutableContext[] = ?ctx <-
  Value:byId["<<immutable-context>>"] = ?value.

HContext(?hctx),
HContextFromRealHContext[?value] = ?hctx,
ImmutableHContext[] = ?hctx <-
  Value:byId["<<immutable-hcontext>>"] = ?value.


// Create initial objects with their heap contexts
HContext(?hctx),
HContextFromRealHContext[?value] = ?hctx,
SystemThreadGroup(?hctx, ?alloc) <-
  Value:byId["<<jvm-startup>>"] = ?value,
  Value:byId["<<system-thread-group>>"] = ?alloc.

HContext(?hctx),
HContextFromRealHContext[?value] = ?hctx,
MainThreadGroup(?hctx, ?alloc) <-
  Value:byId["<<jvm-startup>>"] = ?value,
  Value:byId["<<main-thread-group>>"] = ?alloc.

HContext(?hctx),
HContextFromRealHContext[?value] = ?hctx,
MainThread(?hctx, ?alloc) <-
  Value:byId["<<jvm-startup>>"] = ?value,
  Value:byId["<<main-thread>>"] = ?alloc.


// Initially reachable methods
Context(?ctx),
ContextFromRealContext[?value] = ?ctx,
ReachableContext(?ctx, ?method) <-
  MainMethodDeclaration(?method),
  Value:byId["<<initial-context>>"] = ?value.

Context(?ctx),
ContextFromRealContext[?value] = ?ctx,
ReachableContext(?ctx, ?method) <-
  ImplicitReachable(?method),
  Value:byId["<<jvm-startup>>"] = ?value.

Context(?ctx),
ContextFromRealContext[?value] = ?ctx,
ReachableContext(?ctx, ?clinit) <-
  InitializedClass(?class),
  ClassInitializer[?class] = ?clinit,
  Value:byId["<<class-initialization>>"] = ?value.
