/**
 * The following rules check whether type S can be cast to type T. Implicitly,
 * they expose the type hierarchy.
 */


/**
 * If S is an ordinary (nonarray) class, then
 * - S must be the same class as T
 * - or a subclass of T
 */
SubtypeOf(?s, ?s),
SupertypeOf(?s, ?s) <-
   ClassType(?s).

SubtypeOf(?s, ?t),
SupertypeOf(?t, ?s) <-
   Subclass(?t, ?s).

/**
 * If T is an interface type, then
 * - S must implement interface T
 */
SubtypeOf(?s, ?t),
SupertypeOf(?t, ?s) <-
   ClassType(?s),
   Superinterface(?t, ?s).

/**
 * If S is an interface type, then
 * - If T is a class type, then T must be Object
 */
SubtypeOf(?s, ?t),
SupertypeOf(?t, ?s) <-
   InterfaceType(?s),
   Type:fqn(?t:"java.lang.Object").

/**
 * If T is an interface type, then
 * - T must be the same interface as S
 * - or a superinterface of S
 */
SubtypeOf(?s, ?s),
SupertypeOf(?s, ?s) <-
   InterfaceType(?s).

SubtypeOf(?s, ?t),
SupertypeOf(?t, ?s) <-
   InterfaceType(?s),
   Superinterface(?t, ?s).

/**
 *  If S is a class representing the array type SC[], that is, an array of
 *  components of type SC, then
 *  - If T is a class type, then T must be Object
 */
SubtypeOf(?s, ?t),
SupertypeOf(?t, ?s) <-
   ArrayType(?s),
   Type:fqn(?t:"java.lang.Object").

/**
 *  If T is an array type TC[], that is, an array of components of type TC,
 *  then one of the following must be true
 *  - TC and SC are the same primitive type
 */
SubtypeOf(?s, ?t),
SupertypeOf(?t, ?s) <-
   ArrayType(?s),
   ArrayType(?t),
   ComponentType[?s] = ?sc,
   ComponentType[?t] = ?sc,
   PrimitiveType(?sc).

/**
 * - TC and SC are reference types (2.4.6), and type SC can be cast to TC by
 * recursive application of these rules
 */
SubtypeOf(?s, ?t),
SupertypeOf(?t, ?s) <-
   ComponentType[?s] = ?sc,
   ComponentType[?t] = ?tc,
   ReferenceType(?sc),
   ReferenceType(?tc),
   SubtypeOf(?sc, ?tc).

/**
 *  If T is an interface type, T must be one of the interfaces implemented by
 *  arrays (2.15)
 */
SubtypeOf(?s, ?t),
SupertypeOf(?t, ?s) <-
   ArrayType(?s),
   InterfaceType(?t),
   Type:fqn(?t:"java.lang.Cloneable").

SubtypeOf(?s, ?t),
SupertypeOf(?t, ?s) <-
   ArrayType(?s),
   InterfaceType(?t),
   Type:fqn(?t:"java.io.Serializable").


/**
 *  Null type acts as a bottom type
 */
SubtypeOf(?s, ?t),
SupertypeOf(?t, ?s) <-
   Type:null[] = ?s,
   ReferenceType(?t),
   ?t != ?s.


/**
 * Subtyping among Primitive Types
 */
SubtypeOf(Type:float[], Type:double[]).
SubtypeOf(Type:long[], Type:float[]).
SubtypeOf(Type:int[], Type:long[]).
SubtypeOf(Type:char[], Type:int[]).
SubtypeOf(Type:short[], Type:int[]).
SubtypeOf(Type:byte[], Type:short[]).

SupertypeOf(Type:double[], Type:float[]).
SupertypeOf(Type:float[], Type:long[]).
SupertypeOf(Type:long[], Type:int[]).
SupertypeOf(Type:int[], Type:char[]).
SupertypeOf(Type:int[], Type:short[]).
SupertypeOf(Type:short[], Type:byte[]).
