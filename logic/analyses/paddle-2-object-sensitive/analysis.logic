RealContext1FromContext[?ctx] = ?value1,
RealContext2FromContext[?ctx] = ?value2 <-
  ContextFromRealContext[?value1, ?value2] = ?ctx.

Context(?ctx),
ContextFromRealContext[?value, ?value] = ?ctx,
ImmutableContext(?ctx) <-
  Value:byId["<<immutable-context>>"] = ?value.

HContext(?hctx),
HContextFromRealHContext[] = ?hctx,
ImmutableHContext(?hctx).


// Create initial objects with their heap contexts
HContext(?hctx),
HContextFromRealHContext[] = ?hctx,
SystemThreadGroup(?hctx, ?alloc) <-
  Value:byId["<<system-thread-group>>"] = ?alloc.

HContext(?hctx),
HContextFromRealHContext[] = ?hctx,
MainThreadGroup(?hctx, ?alloc) <-
  Value:byId["<<main-thread-group>>"] = ?alloc.

HContext(?hctx),
HContextFromRealHContext[] = ?hctx,
MainThread(?hctx, ?alloc) <-
  Value:byId["<<main-thread>>"] = ?alloc.


// Merge optimization hack

// For this analysis, we only need two of the parameters that may influence the
// new context object.
MyMergeBasis(?callerCtx, ?value) <-
  MergeBasis(_, _, ?callerCtx, ?value).

// We create new context objects sparingly, because of the high cost.  We also
// cache them, so they can be looked up without a join.
Context(?calleeCtx),
ContextFromRealContext[RealContext2FromContext[?callerCtx], ?value] = ?calleeCtx,
OptimizeMerge[?callerCtx, ?value] = ?calleeCtx <-
  MyMergeBasis(?callerCtx, ?value).


// Initially reachable methods
Context(?ctx),
ContextFromRealContext[?value, ?value] = ?ctx,
ReachableContext(?ctx, ?method) <-
  MainMethodDeclaration(?method),
  Value:byId["<<initial-context>>"] = ?value.

Context(?ctx),
ContextFromRealContext[?value, ?value] = ?ctx,
ReachableContext(?ctx, ?method) <-
  ImplicitReachable(?method),
  Value:byId["<<jvm-startup>>"] = ?value.

Context(?ctx),
ContextFromRealContext[?value, ?value] = ?ctx,
ReachableContext(?ctx, ?clinit) <-
  InitializedClass(?class),
  ClassInitializer[?class] = ?clinit,
  Value:byId["<<class-initialization>>"] = ?value.
